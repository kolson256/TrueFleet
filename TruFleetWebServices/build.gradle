apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = '1.8'

mainClassName = 'com.trufleet.services.TruFleetAPI'
version = '0.1.0'
String dropwizard_version = '0.7.1'

repositories {
    mavenCentral()
    
//    maven{
//        url "http://repository-multifarious.forge.cloudbees.com/snapshot/"
//    }
}

configurations {
    capsule
}

dependencies {

    compile "io.dropwizard:dropwizard-core:$dropwizard_version"
    compile "io.dropwizard:dropwizard-db:$dropwizard_version"
    compile "io.dropwizard:dropwizard-jdbi:$dropwizard_version"
    compile "io.dropwizard:dropwizard-jackson:$dropwizard_version"
    compile "io.dropwizard:dropwizard-logging:$dropwizard_version"
    compile "io.dropwizard:dropwizard-metrics:$dropwizard_version"
    compile "io.dropwizard:dropwizard-hibernate:$dropwizard_version"

    compile "org.secnod.dropwizard:dropwizard-shiro:0.1.1"
    compile "org.hibernate:hibernate-entitymanager:4.3.8.Final"
    compile "org.hibernate:hibernate-core:4.3.8.Final"

    compile "com.google.code.gson:gson:2.2.4"
    compile "com.sun.jersey:jersey-core:1.17.1"
    compile "org.postgresql:postgresql:9.3-1102-jdbc41"
    compile "org.json:json:20090211"

    capsule 'co.paralleluniverse:capsule:0.10.0'

    testCompile 'junit:junit:4.11'
    testCompile "io.dropwizard:dropwizard-testing:$dropwizard_version"
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
    testCompile 'com.ninja-squad:DbSetup:1.3.1'

}
run {
    systemProperty "dropwizard.config", "build/resources/main/webservices-config.yml"
}
task capsule(type: Jar, dependsOn: classes) {
    archiveName = "TruFleetWebServices-Deploy.jar"

    from jar // embed our application jar
    from { configurations.runtime } // embed dependencies

    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' } // we just need the single Capsule class

    from { sourceSets.main.resources }

    manifest {
        attributes(
                'Main-Class'  :   'Capsule',
                'Application-Class'   : mainClassName,
                'Application-Version' : version,
                'Min-Java-Version' : '1.8.0',
                'JVM-Args' : run.jvmArgs.join(' '),
                'System-Properties' : (run.systemProperties + ["dropwizard.config": '$CAPSULE_DIR/webservices-config.yml']).collect { k,v -> "$k=$v" }.join(' '),
        )
    }
}
