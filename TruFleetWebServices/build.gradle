apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = '1.8'

mainClassName = 'com.trufleet.services.TruFleetAPI'
version = '0.1.0'
String dropwizard_version = '0.7.1'

repositories {
    mavenCentral()
    maven{
        url "http://repository-multifarious.forge.cloudbees.com/snapshot/"
    }
}

configurations {
    capsule
}

dependencies {
    //compile 'io.ifar:dw-shiro-bundle:0.0.2-SNAPSHOT'
    compile "io.dropwizard:dropwizard-core:$dropwizard_version"
    compile "io.dropwizard:dropwizard-db:$dropwizard_version"
    compile "io.dropwizard:dropwizard-jdbi:$dropwizard_version"
    compile "io.dropwizard:dropwizard-jackson:$dropwizard_version"
    compile"org.postgresql:postgresql:9.3-1102-jdbc41"
    compile "org.json:json:20090211"

    capsule 'co.paralleluniverse:capsule:0.9.0'

    testCompile 'junit:junit:4.11'
}
run {
    systemProperty "dropwizard.config", "build/resources/main/webservices-config.yml"
    systemProperty "STOP.PORT", "8082"
    systemProperty "STOP.KEY", "stop"
}
task capsule(type: Jar, dependsOn: classes) {
    archiveName = "TruFleetWebServices-Deploy.jar"

    from jar // embed our application jar
    from { configurations.runtime } // embed dependencies

    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' } // we just need the single Capsule class

    from { sourceSets.main.resources }

    manifest {
        attributes(
                'Main-Class'  :   'Capsule',
                'Application-Class'   : mainClassName,
                'Application-Version' : version,
                'Min-Java-Version' : '1.8.0',
                'JVM-Args' : run.jvmArgs.join(' '),
                'System-Properties' : (run.systemProperties + ["dropwizard.config": '$CAPSULE_DIR/webservices-config.yml']).collect { k,v -> "$k=$v" }.join(' '),
        )
    }
}
