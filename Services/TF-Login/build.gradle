apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = '1.8'
mainClassName = 'edu.depaul.truefleet.service.login.LoginMain'
version = '0.1.0'
String shiro_version = '1.2.2'
String stormpath_version = '1.0.RC2.1'
String dropwizard_version = '0.7.1'


repositories {
    mavenCentral()
    maven{
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

configurations {
    capsule
}

dependencies {
    compile "org.apache.shiro:shiro-core:$shiro_version"
    compile "org.apache.shiro:shiro-web:$shiro_version"
    compile "com.stormpath.sdk:stormpath-sdk-api:$stormpath_version"
    compile "com.stormpath.sdk:stormpath-sdk-httpclient:$stormpath_version"
    compile "com.stormpath.shiro:stormpath-shiro-core:0.6.0"
    compile "io.dropwizard:dropwizard-core:$dropwizard_version"
    compile "io.dropwizard:dropwizard-auth:$dropwizard_version"
    capsule 'co.paralleluniverse:capsule:0.9.0'
    testCompile 'junit:junit:4.11'
}
run {
    systemProperty "dropwizard.config", "build/resources/main/login-config.yml"
}

jar {
    manifest {
        attributes("Implementation-Version": stormpath_version)
    }
}

task capsule(type: Jar, dependsOn: classes) {
    archiveName = "TF-Login-Deploy.jar"

    from jar // embed our application jar
    from { configurations.runtime } // embed dependencies

    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' } // we just need the single Capsule class

    from { sourceSets.main.resources }

    manifest {
        attributes(
                'Main-Class'  :   'Capsule',
                'Application-Class'   : mainClassName,
                'Application-Version' : version,
                'Min-Java-Version' : '1.8.0',
                'JVM-Args' : run.jvmArgs.join(' '),
                'System-Properties' : (run.systemProperties + ["dropwizard.config": '$CAPSULE_DIR/login-config.yml']).collect { k,v -> "$k=$v" }.join(' '),
        )
    }
}
